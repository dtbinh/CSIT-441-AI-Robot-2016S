import org.gradle.internal.os.OperatingSystem;

group 'edu.unk'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'org.hidetake.ssh'

sourceCompatibility = 1.7
version = '1.0'
compileJava.options.encoding = 'UTF-8'
mainClassName = 'Main'

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle Lego/Lejos Mindstorms EV3 Example Project', 'Implementation-Version': version, 'Main-Class': mainClassName
    }
    // note this builds one fat jar and it is not recommended for production use - just for illustration purpose
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.hidetake:gradle-ssh-plugin:1.4.0'
    }
}

configurations {
    sshAntTask
    provided
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
}

repositories {
    mavenCentral()
    if (OperatingSystem.current().isWindows()) {
        flatDir {
            dirs System.getenv("EV3_HOME") + '/lib/ev3'
        }
    } else if (OperatingSystem.current().isMacOsX()) {
        flatDir {
            dirs '/Users/michael/bin/leJOS_EV3_0.9.1-beta' + '/lib/ev3'
        }
    } else if (OperatingSystem.current().isLinux()) {
        flatDir {
            dirs System.getenv("EV3_HOME") + '/lib/ev3'
        }
    }
}

dependencies {
    sshAntTask 'org.apache.ant:ant-jsch:1.7.1', 'jsch:jsch:0.1.29'
    compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
    provided group: 'lejos', name: 'ev3classes', version: '1.0'
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

test {
    systemProperties 'property': 'value'
}

task deployEV3Ant << {
    description = 'Update files on remote server.'

    // Get passphrase from user input.
    def console = System.console()
    def passphrase = 'root'

    // Redefine scp Ant task, with the classpath property set to our newly defined
    // sshAntTask configuration classpath.
    ant.taskdef(name: 'scp', classname: 'org.apache.tools.ant.taskdefs.optional.ssh.Scp',
            classpath: configurations.sshAntTask.asPath)

    // Invoke the scp Ant task. (Use gradle -i update to see the output of the Ant task.)
    ant.scp(todir: 'root@192.168.0.10:/home/lejos/programs',
            keyfile: 'keys/id_rsa',
            passphrase: passphrase as String, // Use phassphrase entered by the user.
            verbose: 'true') {
        fileset(dir: 'build/libs') {
            include(name: '**/**')
        }
    }
}